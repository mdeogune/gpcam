apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "info.gps360.gpcam"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.00'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"
    productFlavors {
        regular
        google
        hidden {
            applicationId "info.gps360.gpcam.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.1'
    googleImplementation 'com.google.firebase:firebase-core:16.0.6'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    googleImplementation "com.google.android.gms:play-services-location:16.0.0"

    implementation project(':liveVideoBroadcasterSDK')

}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    task copyJson(type: Copy) {
        from '../../gps360-shared/google-services'
        into '.'
        include 'gps360-gpcam-android.json'
        rename('gps360-gpcam-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }


}
